SET QUOTED_IDENTIFIER OFF 
GO
SET ANSI_NULLS OFF 
GO

if exists (select * from dbo.sysobjects where id = object_id(N'[dbo].[fnLastHumanCase]') and xtype in (N'FN', N'IF', N'TF'))
drop function [dbo].[fnLastHumanCase]
GO

--##SUMMARY Returns id of the last human case entered at the specified site.

--##REMARKS Author: Mirnaya O.
--##REMARKS Create date: 28.01.2010

--##RETURNS Function returns id of the last human case entered at the specified site.


/*
--Example of a call of function:
declare @idfsSite	bigint
select dbo.fnLastHumanCase (@idfsSite)

*/


create	function	fnLastHumanCase
(
	@idfsSite as bigint --##PARAM @idfsSite Site Id
)
returns bigint
as
begin

declare @res bigint
set @res = null

select			@res = tlbFirstHumanCase.idfHumanCase
from			tlbHumanCase as tlbFirstHumanCase
inner join		tlbCase as tlbFirstCase
on				tlbFirstCase.idfCase = tlbFirstHumanCase.idfHumanCase
				and tlbFirstCase.intRowStatus = 0
inner join		(
	tlbParty as tlbFirstParty
	inner join	tlbHuman as tlbFirstHuman
	on			tlbFirstHuman.idfHuman = tlbFirstParty.idfParty
				)
on				tlbFirstParty.idfCase = tlbFirstHumanCase.idfHumanCase
				and tlbFirstParty.idfsPartyType = 10072006			-- Human
				and tlbFirstParty.intRowStatus = 0
left join		(
	tlbHumanCase as tlbMaxHumanCase
	inner join		tlbCase as tlbMaxCase
	on				tlbMaxCase.idfCase = tlbMaxHumanCase.idfHumanCase
					and tlbMaxCase.intRowStatus = 0
	inner join		(
		tlbParty as tlbMaxParty
		inner join	tlbHuman as tlbMaxHuman
		on			tlbMaxHuman.idfHuman = tlbMaxParty.idfParty
					)
	on				tlbMaxParty.idfCase = tlbMaxHumanCase.idfHumanCase
					and tlbMaxParty.idfsPartyType = 10072006		-- Human
					and tlbMaxParty.intRowStatus = 0
				)
on				tlbMaxCase.idfsSite = @idfsSite
				and	(	(tlbMaxCase.datEnteredDate > tlbFirstCase.datEnteredDate)
						or ((tlbMaxCase.datEnteredDate = tlbFirstCase.datEnteredDate)
							and (tlbMaxCase.strCaseID > tlbFirstCase.strCaseID))
					)
where			tlbFirstCase.idfsSite = @idfsSite
				and tlbMaxHumanCase.idfHumanCase is null

return @res
end


GO
SET QUOTED_IDENTIFIER OFF 
GO
SET ANSI_NULLS ON 
GO

