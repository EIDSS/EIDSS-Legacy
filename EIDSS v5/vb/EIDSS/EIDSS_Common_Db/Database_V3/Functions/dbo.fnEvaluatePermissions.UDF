
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[fnEvaluatePermissions]') AND type in (N'FN', N'IF', N'TF', N'FS', N'FT'))
DROP FUNCTION [dbo].[fnEvaluatePermissions]
GO

/*
	select * from fnEvaluatePermissions(1)
*/

CREATE FUNCTION dbo.fnEvaluatePermissions
(	
	@idfEmployee bigint
)
RETURNS TABLE 
AS
RETURN 
(
select		trtSystemFunction.idfsSystemFunction,
			trtBaseReference.strBaseReferenceCode,
			trtObjectTypeToObjectOperation.idfsObjectOperation,
			COALESCE(
						min(InstanceUser.intPermission),
						min(InstanceDefault.intPermission), 
						min(TypeUser.intPermission),
						min(TypeDefault.intPermission),
						2
					) as intPermission
from		trtSystemFunction
inner join	trtBaseReference
on			trtBaseReference.idfsBaseREference=trtSystemFunction.idfsSystemFunction
inner join	trtObjectTypeToObjectOperation
on			trtSystemFunction.idfsObjectType=trtObjectTypeToObjectOperation.idfsObjectType
--user rights on instance
left join
(
			select		tstObjectAccess.idfsObjectID,
						tstObjectAccess.idfsObjectOperation,
						tstObjectAccess.intPermission
			from		tstObjectAccess
			inner join	fn_ObjectActorRelations(@idfEmployee) Groups
			on			tstObjectAccess.idfActor=Groups.idfEmployee and
						tstObjectAccess.intRowStatus=0 and
						tstObjectAccess.idfsOnSite=dbo.fnSiteID()
)InstanceUser
on			trtSystemFunction.idfsSystemFunction=InstanceUser.idfsObjectID and
			trtObjectTypeToObjectOperation.idfsObjectOperation=InstanceUser.idfsObjectOperation
--default rights on instance
left join
(
			select		tstObjectAccess.idfsObjectID,
						tstObjectAccess.idfsObjectOperation,
						tstObjectAccess.intPermission
			from		tstObjectAccess
			where		tstObjectAccess.idfActor=-1 and
						tstObjectAccess.intRowStatus=0 and
						tstObjectAccess.idfsOnSite=dbo.fnSiteID()
)InstanceDefault
on			trtSystemFunction.idfsSystemFunction=InstanceDefault.idfsObjectID and
			trtObjectTypeToObjectOperation.idfsObjectOperation=InstanceDefault.idfsObjectOperation
--user rights on type
left join
(
			select		tstObjectAccess.idfsObjectOperation,
						tstObjectAccess.idfsObjectType,
						tstObjectAccess.intPermission
			from		tstObjectAccess
			inner join	fn_ObjectActorRelations(@idfEmployee) Groups
			on			tstObjectAccess.idfActor=Groups.idfEmployee and
						tstObjectAccess.intRowStatus=0 and
						tstObjectAccess.idfsObjectID is null and
						tstObjectAccess.idfsOnSite=dbo.fnSiteID()

)TypeUser
on			trtSystemFunction.idfsObjectType=TypeUser.idfsObjectType and
			trtObjectTypeToObjectOperation.idfsObjectOperation=TypeUser.idfsObjectOperation
--default rights on type
left join
(
			select		tstObjectAccess.idfsObjectOperation,
						tstObjectAccess.idfsObjectType,
						tstObjectAccess.intPermission
			from		tstObjectAccess
			WHERE		tstObjectAccess.idfActor=-1 and
						tstObjectAccess.intRowStatus=0 and
						tstObjectAccess.idfsObjectID is null and
						tstObjectAccess.idfsOnSite=dbo.fnSiteID()
)TypeDefault
on			trtSystemFunction.idfsObjectType=TypeDefault.idfsObjectType and
			trtObjectTypeToObjectOperation.idfsObjectOperation=TypeDefault.idfsObjectOperation

where		trtSystemFunction.intRowStatus=0 and
			trtBaseReference.intRowStatus=0 and
			trtObjectTypeToObjectOperation.intRowStatus=0

group by	trtSystemFunction.idfsSystemFunction,
			trtBaseReference.strBaseReferenceCode,
			trtObjectTypeToObjectOperation.idfsObjectOperation
)
GO
