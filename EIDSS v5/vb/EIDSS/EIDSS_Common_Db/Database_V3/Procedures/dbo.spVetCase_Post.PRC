SET QUOTED_IDENTIFIER OFF 
GO
SET ANSI_NULLS OFF 
GO

if exists (select * from dbo.sysobjects where id = object_id(N'[dbo].[spVetCase_Post]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
drop procedure [dbo].[spVetCase_Post]
GO

--##SUMMARY Posts general Veterinary case data.
--##SUMMARY Wet case can't be deleted using this procedure, spVetCase_Delete procedure is used for this purpose.
--##SUMMARY This procedure is usable for both avian and livestock cases.

--##REMARKS Author: Zurin M.
--##REMARKS Create date: 22.11.2009

--##RETURNS Doesn't use


/*
--Example of a call of procedure:
DECLARE @idfCase bigint
DECLARE @idfsCaseStatus bigint
DECLARE @idfsShowDiagnosis bigint
DECLARE @idfsCaseType bigint
DECLARE @datEnteredDate datetime
DECLARE @strCaseID nvarchar(200)
DECLARE @idfsTentativeDiagnosis bigint
DECLARE @idfsTentativeDiagnosis1 bigint
DECLARE @idfsTentativeDiagnosis2 bigint
DECLARE @idfsFinalDiagnosis bigint
DECLARE @idfPersonInvestigatedBy bigint
DECLARE @idfPersonEnteredBy bigint
DECLARE @idfPersonReportedBy bigint
DECLARE @idfObservation bigint
DECLARE @idfsSite bigint
DECLARE @datReportDate datetime
DECLARE @datAssignedDate datetime
DECLARE @datInvestigationDate datetime
DECLARE @datTentativeDiagnosisDate datetime
DECLARE @datTentativeDiagnosis1Date datetime
DECLARE @datTentativeDiagnosis2Date datetime
DECLARE @datFinalDiagnosisDate datetime
DECLARE @strSampleNotes nvarchar(1000)
DECLARE @strTestNotes nvarchar(1000)
DECLARE @strSummaryNotes nvarchar(1000)
DECLARE @strClinicalNotes nvarchar(1000)
DECLARE @strFieldAccessionID nvarchar(200)
DECLARE @idfsFormTemplate bigint


EXECUTE spVetCase_Post 
   @idfCase
  ,@idfsCaseStatus
  ,@idfsShowDiagnosis
  ,@idfsCaseType
  ,@datEnteredDate
  ,@strCaseID
  ,@idfsTentativeDiagnosis
  ,@idfsTentativeDiagnosis1
  ,@idfsTentativeDiagnosis2
  ,@idfsFinalDiagnosis
  ,@idfPersonInvestigatedBy
  ,@idfPersonEnteredBy
  ,@idfPersonReportedBy
  ,@idfObservation
  ,@idfsSite
  ,@datReportDate
  ,@datAssignedDate
  ,@datInvestigationDate
  ,@datTentativeDiagnosisDate
  ,@datTentativeDiagnosis1Date
  ,@datTentativeDiagnosis2Date
  ,@datFinalDiagnosisDate
  ,@strSampleNotes
  ,@strTestNotes
  ,@strSummaryNotes
  ,@strClinicalNotes
  ,@strFieldAccessionID
  ,@idfsFormTemplate
*/


Create            Proc	spVetCase_Post
	 @idfCase						bigint --##PARAM @idfCase - case ID
	,@idfOutbreak					bigint --##PARAM @idfOutbreak Id of the outbreak, which includes a case (reference to tlbOutbreak)
	,@idfsCaseStatus				bigint --##PARAM @idfsCaseStatus - case classification, reference to rftCaseStatus (19000011)
	,@idfsCaseProgressStatus		bigint --##PARAM @idfsCaseProgressStatus - case progress status, reference to (19000111)
	,@idfsShowDiagnosis				bigint --##PARAM @idfsShowDiagnosis - current case diagnosis, calcualted befor case posting.
	,@idfsCaseType					bigint --##PARAM @idfsCaseType - case type, reference to rftCaseType (19000012). Can be Livestock(10012003) or Avian(10012004)
	,@datEnteredDate				DateTime --##PARAM @datEnteredDate - date when case is entered inot system
	,@strCaseID						NVARCHAR(200) --##PARAM @strCaseID - unique human readable case identifier, generated by system
	,@idfsTentativeDiagnosis		bigint --##PARAM @idfsTentativeDiagnosis - case tentative diagnosis
	,@idfsTentativeDiagnosis1		bigint --##PARAM @idfsTentativeDiagnosis1 - case tentative diagnosis 1
	,@idfsTentativeDiagnosis2		bigint --##PARAM @idfsTentativeDiagnosis2 - case tentative diagnosis 2
	,@idfsFinalDiagnosis			bigint --##PARAM @idfsFinalDiagnosis - case final diagnosis
	,@idfPersonInvestigatedBy		bigint --##PARAM @idfPersonInvestigatedBy - reference to person that investigated case 
	,@idfPersonEnteredBy			bigint --##PARAM @idfPersonEnteredBy - reference to person that entered case into system
	,@idfPersonReportedBy			bigint --##PARAM @idfPersonReportedBy- reference to person that repored about case
	,@idfObservation				bigint --##PARAM @idfObservation - reference to flexible form containing case control measures
	,@idfsFormTemplate				BIGINT --##PARAM @idfsFormTemplate - reference to template that was used last time for control measures flexible form 
	,@idfsSite						bigint --##PARAM @idfsSite - site where case was entered. If not defined by client app, the current system site is used
	,@datReportDate					DateTime --##PARAM @datReportDate - date when case was reported
	,@datAssignedDate				DateTime --##PARAM @datAssignedDate - date when case was assigned for investigation
	,@datInvestigationDate			DateTime --##PARAM @datInvestigationDate - date when case was investigated
	,@datTentativeDiagnosisDate		DateTime --##PARAM @datTentativeDiagnosisDate - date of tentative diagnosis
	,@datTentativeDiagnosis1Date	DateTime --##PARAM @datTentativeDiagnosis1Date - date of tentative diagnosis 1
	,@datTentativeDiagnosis2Date	DateTime --##PARAM @datTentativeDiagnosis2Date - date of tentative diagnosis 2
	,@datFinalDiagnosisDate			DateTime --##PARAM @datFinalDiagnosisDate - date of final diagnosis
	,@strSampleNotes				NVARCHAR(1000) --##PARAM @strSampleNotes - arbitraty text data related with case specimens
	,@strTestNotes					NVARCHAR(1000) --##PARAM @strTestNotes - arbitraty text data related with case tests
	,@strSummaryNotes				NVARCHAR(1000) --##PARAM @strSummaryNotes - arbitraty text data related with case itself
	,@strClinicalNotes				NVARCHAR(1000) --##PARAM @strClinicalNotes - currently is not used
	,@strFieldAccessionID			NVARCHAR(200) --##PARAM @strFieldAccessionID - nique human readable case identifier assigned to case at field

As

declare @check int
IF Exists (SELECT idfCase FROM dbo.tlbCase WHERE idfCase = @idfCase)
BEGIN
	execute @check=spCheckPermission @idfsSystemFunction=10094028,@idfsObjectOperation=10059004,@idfObjectID=@idfCase --vet,write
END
else
BEGIN
	execute @check=spCheckPermission @idfsSystemFunction=10094028,@idfsObjectOperation=10059001,@idfObjectID=@idfCase --vet,create
END	
if @check<>1 return -1


--Calculate case diagnosis if it was not calcualted by client application
If @idfsShowDiagnosis IS NULL AND ((NOT @idfsTentativeDiagnosis IS NULL) OR (NOT @idfsTentativeDiagnosis1 IS NULL) OR (NOT @idfsTentativeDiagnosis2 IS NULL) OR (NOT @idfsFinalDiagnosis IS NULL))
BEGIN
	IF (NOT @idfsFinalDiagnosis IS NULL)
		SET @idfsShowDiagnosis = @idfsFinalDiagnosis
	ELSE
	BEGIN
		DECLARE @DiagnosisDate DATETIME
		IF (NOT @idfsTentativeDiagnosis2 IS NULL)
		BEGIN
			SET @idfsShowDiagnosis = @idfsTentativeDiagnosis2
			SET @DiagnosisDate = @datTentativeDiagnosis2Date
		END
		IF (NOT @idfsTentativeDiagnosis1 IS NULL) AND (@idfsShowDiagnosis IS NULL OR @datTentativeDiagnosis1Date>@DiagnosisDate)
		BEGIN
			SET @idfsShowDiagnosis = @idfsTentativeDiagnosis1
			SET @DiagnosisDate = @datTentativeDiagnosis1Date
		END
		IF (NOT @idfsTentativeDiagnosis IS NULL) AND (@idfsShowDiagnosis IS NULL OR @datTentativeDiagnosisDate>@DiagnosisDate)
			SET @idfsShowDiagnosis = @idfsTentativeDiagnosis
	END
END
if not exists	(
	select	*
	from	tlbOutbreak Outbreak
	where	Outbreak.idfOutbreak = @idfOutbreak
			and Outbreak.intRowStatus = 0
				)
begin
	set @idfOutbreak = null
end

IF Exists (SELECT idfCase FROM dbo.tlbCase WHERE idfCase = @idfCase)
BEGIN
	UPDATE tlbCase
	SET
		idfsCaseStatus=@idfsCaseStatus,
		idfsCaseProgressStatus=@idfsCaseProgressStatus,
		idfOutbreak=@idfOutbreak,
		idfsShowDiagnosis=@idfsShowDiagnosis,
		idfsCaseType=@idfsCaseType,
		datEnteredDate=@datEnteredDate,
		strCaseID=@strCaseID,
		strSampleNotes = @strSampleNotes
	WHERE
		idfCase = @idfCase
END
ELSE
BEGIN
	IF ISNULL(@strCaseID,N'') = N'' OR LEFT(ISNULL(@strCaseID,N''),4)='(new'
	BEGIN
		EXEC dbo.spGetNextNumber 10057024, @strCaseID OUTPUT , NULL --N'Vet Case'
	END
	INSERT INTO tlbCase (
		idfCase,
		idfsCaseType,
		idfsCaseStatus,
		idfsCaseProgressStatus,
		idfsShowDiagnosis,
		datEnteredDate,
		strCaseID,
		idfOutbreak,
		strSampleNotes
	)
	VALUES (
		@idfCase,
		@idfsCaseType,
		@idfsCaseStatus,
		@idfsCaseProgressStatus,
		@idfsShowDiagnosis,
		@datEnteredDate,
		@strCaseID,
		@idfOutbreak,
		@strSampleNotes
	)
END
IF Exists (SELECT idfVetCase FROM dbo.tlbVetCase WHERE idfVetCase = @idfCase)
BEGIN
	UPDATE tlbVetCase
	   SET idfVetCase = @idfCase
		  ,idfsTentativeDiagnosis = @idfsTentativeDiagnosis
		  ,idfsTentativeDiagnosis1 = @idfsTentativeDiagnosis1
		  ,idfsTentativeDiagnosis2 = @idfsTentativeDiagnosis2
		  ,idfsFinalDiagnosis = @idfsFinalDiagnosis
		  ,idfPersonInvestigatedBy = @idfPersonInvestigatedBy
		  ,idfPersonEnteredBy = @idfPersonEnteredBy
		  ,idfPersonReportedBy = @idfPersonReportedBy
		  ,idfObservation = @idfObservation
		  ,idfsSite = @idfsSite
		  ,datReportDate = @datReportDate
		  ,datAssignedDate = @datAssignedDate
		  ,datInvestigationDate = @datInvestigationDate
		  ,datTentativeDiagnosisDate = @datTentativeDiagnosisDate
		  ,datTentativeDiagnosis1Date = @datTentativeDiagnosis1Date
		  ,datTentativeDiagnosis2Date = @datTentativeDiagnosis2Date
		  ,datFinalDiagnosisDate = @datFinalDiagnosisDate
		  ,strTestNotes = @strTestNotes
		  ,strSummaryNotes = @strSummaryNotes
		  ,strClinicalNotes = @strClinicalNotes
		  ,strFieldAccessionID = @strFieldAccessionID
	WHERE
			idfVetCase = @idfCase
END
ELSE
BEGIN
	INSERT INTO tlbObservation
           (
			idfObservation
           ,idfsFormTemplate
			)
     VALUES
           (
			@idfObservation
           ,@idfsFormTemplate
			)

	INSERT INTO tlbVetCase
           (idfVetCase
           ,idfsTentativeDiagnosis
           ,idfsTentativeDiagnosis1
           ,idfsTentativeDiagnosis2
           ,idfsFinalDiagnosis
           ,idfPersonInvestigatedBy
           ,idfPersonEnteredBy
           ,idfPersonReportedBy
           ,idfObservation
           ,idfsSite
           ,datReportDate
           ,datAssignedDate
           ,datInvestigationDate
           ,datTentativeDiagnosisDate
           ,datTentativeDiagnosis1Date
           ,datTentativeDiagnosis2Date
           ,datFinalDiagnosisDate
           ,strTestNotes
           ,strSummaryNotes
           ,strClinicalNotes
           ,strFieldAccessionID
           )
     VALUES
           (@idfCase
           ,@idfsTentativeDiagnosis
           ,@idfsTentativeDiagnosis1
           ,@idfsTentativeDiagnosis2
           ,@idfsFinalDiagnosis
           ,@idfPersonInvestigatedBy
           ,@idfPersonEnteredBy
           ,@idfPersonReportedBy
           ,@idfObservation
           ,ISNULL(@idfsSite,dbo.fnSiteID())
           ,@datReportDate
           ,@datAssignedDate
           ,@datInvestigationDate
           ,@datTentativeDiagnosisDate
           ,@datTentativeDiagnosis1Date
           ,@datTentativeDiagnosis2Date
           ,@datFinalDiagnosisDate
           ,@strTestNotes
           ,@strSummaryNotes
           ,@strClinicalNotes
           ,@strFieldAccessionID
           )
END

return 0

GO
SET QUOTED_IDENTIFIER OFF 
GO
SET ANSI_NULLS ON 
GO

