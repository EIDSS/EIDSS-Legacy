SET QUOTED_IDENTIFIER OFF 
GO
SET ANSI_NULLS OFF 
GO

if exists (select * from dbo.sysobjects where id = object_id(N'[dbo].[spHumanCase_Post]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
drop procedure [dbo].[spHumanCase_Post]
GO

--##SUMMARY Saves information about the human case.

--##REMARKS Author: Mirnaya O.
--##REMARKS Update date: 28.01.2010

--##RETURNS Doesn't use


/*
--Example of a call of procedure:
declare	@idfCase						bigint
declare	@idfOutbreak					bigint
declare	@datEnteredDate					datetime
declare	@strCaseID						nvarchar(200)
declare	@idfsCaseProgressStatus			bigint
declare	@idfsFinalState					bigint
declare	@idfsHospitalizationStatus		bigint
declare	@idfsHumanAgeType				bigint
declare	@idfsYNAntimicrobialTherapy		bigint
declare	@idfsYNHospitalization			bigint
declare	@idfsYNSpecimenCollected		bigint
declare	@idfsYNRelatedToOutbreak		bigint
declare	@idfsOutcome					bigint
declare	@idfsTentativeDiagnosis			bigint
declare	@idfsFinalDiagnosis				bigint
declare	@idfsInitialCaseStatus			bigint
declare	@idfsFinalCaseStatus			bigint
declare	@idfSentByOffice				bigint
declare	@idfReceivedByOffice			bigint
declare	@idfInvestigatedByOffice		bigint
declare	@idfPointGeoLocation			bigint
declare	@idfEpiObservation				bigint
declare	@idfsEPIFormTemplate			bigint
declare	@idfCSObservation				bigint
declare	@idfsCSFormTemplate				bigint
declare	@datNotificationDate			datetime
declare	@datCompletionPaperFormDate		datetime
declare	@datFirstSoughtCareDate			datetime
declare	@datModificationDate			datetime
declare	@datHospitalizationDate			datetime
declare	@datFacilityLastVisit			datetime
declare	@datExposureDate				datetime
declare	@datDischargeDate				datetime
declare	@datOnsetDate					datetime
declare	@datInvestigationStartDate		datetime
declare	@datTentativeDiagnosisDate		datetime
declare	@datFinalDiagnosisDate			datetime
declare	@strNote						nvarchar(2000)
declare	@strCurrentLocation				nvarchar(200)
declare	@strHospitalizationPlace		nvarchar(200)
declare	@strLocalIdentifier				nvarchar(200)
declare	@strSoughtCareFacility			nvarchar(200)
declare	@strSentByFirstName				nvarchar(200)
declare	@strSentByPatronymicName		nvarchar(200)
declare	@strSentByLastName				nvarchar(200)
declare	@strReceivedByFirstName			nvarchar(200)
declare	@strReceivedByPatronymicName	nvarchar(200)
declare	@strReceivedByLastName			nvarchar(200)
declare	@strEpidemiologistsName			nvarchar(200)
declare	@strNotCollectedReason			nvarchar(200)
declare	@strClinicalDiagnosis			nvarchar(200)
declare	@intPatientAge					int
declare	@blnClinicalDiagBasis			bit
declare	@blnLabDiagBasis				bit
declare	@blnEpiDiagBasis				bit
declare	@strClinicalNotes				nvarchar(2000)
declare	@strSummaryNotes				nvarchar(2000)
declare	@idfHuman						bigint
declare	@idfsOccupationType				bigint
declare	@idfRegistrationAddress			bigint
declare	@datDateOfDeath					datetime
declare	@strRegistrationPhone			nvarchar(200)
declare	@strWorkPhone					nvarchar(200)


exec spHumanCase_Post 
	 @idfCase
	,@idfOutbreak
	,@datEnteredDate
	,@strCaseID
	,@idfsCaseProgressStatus
	,@idfsFinalState
	,@idfsHospitalizationStatus
	,@idfsHumanAgeType
	,@idfsYNAntimicrobialTherapy
	,@idfsYNHospitalization
	,@idfsYNSpecimenCollected
	,@idfsYNRelatedToOutbreak
	,@idfsOutcome
	,@idfsTentativeDiagnosis
	,@idfsFinalDiagnosis
	,@idfsInitialCaseStatus
	,@idfsFinalCaseStatus
	,@idfSentByOffice
	,@idfReceivedByOffice
	,@idfInvestigatedByOffice
	,@idfPointGeoLocation
	,@idfEpiObservation
	,@idfsEPIFormTemplate
	,@idfCSObservation
	,@idfsCSFormTemplate
	,@datNotificationDate
	,@datCompletionPaperFormDate
	,@datFirstSoughtCareDate
	,@datModificationDate
	,@datHospitalizationDate
	,@datFacilityLastVisit
	,@datExposureDate
	,@datDischargeDate
	,@datOnsetDate
	,@datInvestigationStartDate
	,@datTentativeDiagnosisDate
	,@datFinalDiagnosisDate
	,@strNote
	,@strCurrentLocation
	,@strHospitalizationPlace
	,@strLocalIdentifier
	,@strSoughtCareFacility
	,@strSentByFirstName
	,@strSentByPatronymicName
	,@strSentByLastName
	,@strReceivedByFirstName
	,@strReceivedByPatronymicName
	,@strReceivedByLastName
	,@strEpidemiologistsName
	,@strNotCollectedReason
	,@strClinicalDiagnosis
	,@intPatientAge
	,@blnClinicalDiagBasis
	,@blnLabDiagBasis
	,@blnEpiDiagBasis
	,@strClinicalNotes
	,@strSummaryNotes
	,@idfHuman
	,@idfsOccupationType
	,@idfRegistrationAddress
	,@datDateOfDeath
	,@strRegistrationPhone
	,@strWorkPhone
*/


create	procedure	spHumanCase_Post
(	 @idfCase						bigint			--##PARAM @idfCase Case Id
	,@idfOutbreak					bigint			--##PARAM @idfOutbreak Id of the outbreak, which includes a case (reference to tlbOutbreak)
	,@datEnteredDate				datetime		--##PARAM @datEnteredDate - date when case is entered in the system
	,@strCaseID						nvarchar(200)	--##PARAM @strCaseID Unique human readable code generated by the system
	,@idfsCaseProgressStatus		bigint			--##PARAM @idfsCaseProgressStatus - Id of case progress status (reference to rftCaseProgressStatus)
	,@idfsFinalState				bigint			--##PARAM @idfsFinalState Id of the status of the patient at time of notification (reference to trtBaseReference with type rftFinalState (19000035))
	,@idfsHospitalizationStatus		bigint			--##PARAM @idfsHospitalizationStatus Id of the current patient location type (reference to trtBaseReference with type rftHospStatus (19000041))
	,@idfsHumanAgeType				bigint			--##PARAM @idfsHumanAgeType Id of the patient age unit (reference to trtBaseReference with type rftHumanAgeType (19000042))
	,@idfsYNAntimicrobialTherapy	bigint			--##PARAM @idfsYNAntimicrobialTherapy Id of the value indicating whether antibiotics were assigned (reference to trtBaseReference with type rftYesNoValue (19000100))
	,@idfsYNHospitalization			bigint			--##PARAM @idfsYNHospitalization Id of the value indicating whether patient was hospitalized (reference to trtBaseReference with type rftYesNoValue (19000100))
	,@idfsYNSpecimenCollected		bigint			--##PARAM @idfsYNSpecimenCollected Id of the value indicating whether specimens were collected (reference to trtBaseReference with type rftYesNoValue (19000100))
	,@idfsYNRelatedToOutbreak		bigint			--##PARAM @idfsYNRelatedToOutbreak Id of the value indicating whether case is included into the outbreak (reference to trtBaseReference with type rftYesNoValue (19000100))
	,@idfsOutcome					bigint			--##PARAM @idfsOutcome Id of the case outcome (reference to trtBaseReference with type rftOutcome (19000064))
	,@idfsTentativeDiagnosis		bigint			--##PARAM @idfsTentativeDiagnosis Id of the initial diagnosis (reference to trtDiagnosis)
	,@idfsFinalDiagnosis			bigint			--##PARAM @idfsFinalDiagnosis Id of the changed diagnosis (reference to trtDiagnosis)
	,@idfsInitialCaseStatus			bigint			--##PARAM @idfsInitialCaseStatus Id of the initial case classification (reference to trtBaseReference with type rftCaseStatus (19000011))
	,@idfsFinalCaseStatus			bigint			--##PARAM @idfsFinalCaseStatus Id of the final case classification (reference to trtBaseReference with type rftCaseStatus (19000011))
	,@idfSentByOffice				bigint			--##PARAM @idfSentByOffice Id of the organization that sent the notification (reference to tlbOffice)
	,@idfReceivedByOffice			bigint			--##PARAM @idfReceivedByOffice Id of the organization that received the notification (reference to tlbOffice)
	,@idfInvestigatedByOffice		bigint			--##PARAM @idfInvestigatedByOffice Id of the organization that conducted the investigation (reference to tlbOffice)
	,@idfPointGeoLocation			bigint			--##PARAM @idfPointGeoLocation Id of the case geografical location (reference to tlbGeoLocation)
	,@idfEpiObservation				bigint			--##PARAM @idfEpiObservation Id of the observation related to the epidemiological investigation of the case (reference to tlbObservation)
	,@idfsEPIFormTemplate			bigint			--##PARAM @idfsEPIFormTemplate Id of the flexible form template for the epidemiological investigations (reference to ffFormTemplate)
	,@idfCSObservation				bigint			--##PARAM @idfCSObservation Id of the observations related to the clinical signs of the case (reference to tlbObservation)
	,@idfsCSFormTemplate			bigint			--##PARAM @idfsCSFormTemplate Id of the flexible form template for the clinical signs (reference to ffFormTemplate)
	,@datNotificationDate			datetime		--##PARAM @datNotificationDate Date of notification
	,@datCompletionPaperFormDate	datetime		--##PARAM @datCompletionPaperFormDate Date of completion of paper form
	,@datFirstSoughtCareDate		datetime		--##PARAM @datFirstSoughtCareDate Date the patient first sought care
	,@datModificationDate			datetime		--##PARAM @datModificationDate Date of last case modification
	,@datHospitalizationDate		datetime		--##PARAM @datHospitalizationDate Date of hospitalization (makes sense only if the @idfsYNHospitalization parameter is valued "Yes" (10100001))
	,@datFacilityLastVisit			datetime		--##PARAM @datFacilityLastVisit Date of last presence at work, study and preschool institution
	,@datExposureDate				datetime		--##PARAM @datExposure Date Date of exposure
	,@datDischargeDate				datetime		--##PARAM @datDischarge Date Date of discharge
	,@datOnsetDate					datetime		--##PARAM @datOnsetDate Date of onset
	,@datInvestigationStartDate		datetime		--##PARAM @datInvestigationStartDate Investigation start date
	,@datTentativeDiagnosisDate		datetime		--##PARAM @datTentativeDiagnosisDate Date of initial diagnosis
	,@datFinalDiagnosisDate			datetime		--##PARAM @datFinalDiagnosisDate Date of changed diagnosis
	,@strNote						nvarchar(2000)	--##PARAM @strNote Additional information/Comments including movement of patient during period of maximum incubation
	,@strCurrentLocation			nvarchar(200)	--##PARAM @strCurrentLocation Current location of the patient
	,@strHospitalizationPlace		nvarchar(200)	--##PARAM @strHospitalizationPlace Place of hospitalization (makes sense only if the @idfsYNHospitalization parameter is valued "Yes" (10100001))
	,@strLocalIdentifier			nvarchar(200)	--##PARAM @strLocalIdentifier Local identifier of the case, created by the user
	,@strSoughtCareFacility			nvarchar(200)	--##PARAM @strSoughtCareFacility Facility where the patient first sought care
	,@strSentByFirstName			nvarchar(200)	--##PARAM @strSentByFirstName First name of the employee that sent the notification
	,@strSentByPatronymicName		nvarchar(200)	--##PARAM @strSentByPatronymicName Second name of the employee that sent the notification
	,@strSentByLastName				nvarchar(200)	--##PARAM @strSentByLastName Last name of the employee that sent the notification
	,@strReceivedByFirstName		nvarchar(200)	--##PARAM @strReceivedByFirstName First name of the employee that received the notification
	,@strReceivedByPatronymicName	nvarchar(200)	--##PARAM @strReceivedByPatronymicName Second name of the employee that received the notification
	,@strReceivedByLastName			nvarchar(200)	--##PARAM @strReceivedByLastName Last name of the employee that received the notification
	,@strEpidemiologistsName		nvarchar(200)	--##PARAM @strEpidemiologistsName Epidemiologist name
	,@strNotCollectedReason			nvarchar(200)	--##PARAM @strNotCollectedReason The reason that specimens were not collected (makes sense only if the @idfsYNSpecimenCollected parameter is valued "No" (10100002))
	,@strClinicalDiagnosis			nvarchar(200)	--##PARAM @strClinicalDiagnosis Non-notifiable diagnosis from the facility where the patient first sought care
	,@intPatientAge					int				--##PARAM @intPatientAge Age of the patient (numerical value)
	,@blnClinicalDiagBasis			bit				--##PARAM @blnClinicalDiagBasis Value indicating whether the clinical signs are basis of the diagnosis
	,@blnLabDiagBasis				bit				--##PARAM @blnLabDiagBasis Value indicating whether the laboratory tests are basis of the diagnosis
	,@blnEpiDiagBasis				bit				--##PARAM @blnEpiDiagBasis Value indicating whether the epidemiological investigations are basis of the diagnosis
	,@strClinicalNotes				nvarchar(2000)	--##PARAM @strClinicalNotes Comments on the clinical information
	,@strSummaryNotes				nvarchar(2000)	--##PARAM @strSummaryNotes General comments
	,@idfHuman						bigint			--##PARAM @idfHuman Id of the human case patient (reference to tlbHuman)
	,@idfsOccupationType			bigint			--##PARAM @idfsOccupationType Id of the occupation (reference to trtBaseReference with type rftOccupationType (19000061))
	,@idfRegistrationAddress		bigint			--##PARAM @idfRegistrationAddress Id of the registration address (reference to tlbGeoLocation)
	,@datDateOfDeath				datetime		--##PARAM @datDateOfDeath Date of completion of death
	,@strRegistrationPhone			nvarchar(200)	--##PARAM @strRegistrationPhone Phone number of the registration residence
	,@strWorkPhone					nvarchar(200)	--##PARAM @strWorkPhone Phone number of the employer, children's facility, and school
)
as

declare @check int
IF Exists (SELECT idfCase FROM dbo.tlbCase WHERE idfCase = @idfCase)
BEGIN
	execute @check=spCheckPermission @idfsSystemFunction=10094027,@idfsObjectOperation=10059004,@idfObjectID=@idfCase --human,write
END
else
BEGIN
	execute @check=spCheckPermission @idfsSystemFunction=10094027,@idfsObjectOperation=10059001,@idfObjectID=@idfCase --human,create
END	

if @check<>1 return -1

-- Update reference parameters related to tlbCase
if not exists	(
		select	*
		from	trtBaseReference CaseProgressStatus
		where	CaseProgressStatus.idfsBaseReference = @idfsCaseProgressStatus
				and CaseProgressStatus.idfsReferenceType = 19000111	-- rftCaseProgressStatus
				and CaseProgressStatus.intRowStatus = 0
				)
begin
	set	@idfsCaseProgressStatus = null
end

if not exists	(
		select	*
		from	trtBaseReference CaseStatus
		where	CaseStatus.idfsBaseReference = @idfsInitialCaseStatus
				and CaseStatus.idfsReferenceType = 19000011	-- rftCaseStatus
				and CaseStatus.intRowStatus = 0
				)
begin
	set	@idfsInitialCaseStatus = null
end

if not exists	(
		select	*
		from	trtBaseReference CaseStatus
		where	CaseStatus.idfsBaseReference = @idfsFinalCaseStatus
				and CaseStatus.idfsReferenceType = 19000011	-- rftCaseStatus
				and CaseStatus.intRowStatus = 0
				)
begin
	set	@idfsFinalCaseStatus = null
end

if not exists	(
		select		*
		from		trtDiagnosis Diagnosis
		inner join	trtBaseReference DiagnosisBR
		on			DiagnosisBR.idfsBaseReference = Diagnosis.idfsDiagnosis
		where		Diagnosis.idfsDiagnosis = @idfsTentativeDiagnosis
					and Diagnosis.intRowStatus = 0
				)
begin
	set	@idfsTentativeDiagnosis = null
end

if not exists	(
		select		*
		from		trtDiagnosis Diagnosis
		inner join	trtBaseReference DiagnosisBR
		on			DiagnosisBR.idfsBaseReference = Diagnosis.idfsDiagnosis
		where		Diagnosis.idfsDiagnosis = @idfsFinalDiagnosis
					and Diagnosis.intRowStatus = 0
				)
begin
	set	@idfsFinalDiagnosis = null
end

declare	@idfsCaseStatus	bigint
set	@idfsCaseStatus = IsNull(@idfsFinalCaseStatus, @idfsInitialCaseStatus)

declare	@idfsShowDiagnosis	bigint
set @idfsShowDiagnosis = IsNull(@idfsFinalDiagnosis, @idfsTentativeDiagnosis)

declare	@idfsCaseType	bigint
set	@idfsCaseType = null
select	@idfsCaseType = CaseType.idfsBaseReference
from	trtBaseReference CaseType
where	CaseType.idfsBaseReference = 10012001		-- Routine Human Case Report
		and CaseType.idfsReferenceType = 19000012	-- rftCaseType
		and CaseType.intRowStatus = 0

if not exists	(
	select	*
	from	tlbOutbreak Outbreak
	where	Outbreak.idfOutbreak = @idfOutbreak
			and Outbreak.intRowStatus = 0
				)
begin
	set @idfOutbreak = null
end

-- Post tlbCase
if exists	(
	select	*
	from	tlbCase
	where	idfCase = @idfCase
			)
begin
	update	tlbCase
	set		idfsCaseStatus		=	@idfsCaseStatus,
			idfsCaseProgressStatus	=	@idfsCaseProgressStatus,
			idfsShowDiagnosis	=	@idfsShowDiagnosis,
			idfsCaseType		=	@idfsCaseType,
			idfOutbreak			=	@idfOutbreak,
			datEnteredDate		=	@datEnteredDate,
			strCaseID			=	@strCaseID

	where	idfCase = @idfCase
end
else begin
	insert into	tlbCase
	(	idfCase,
		idfsCaseStatus,
		idfsCaseProgressStatus,
		idfsShowDiagnosis,
		idfsCaseType,
		idfOutbreak,
		datEnteredDate,
		strCaseID
	)
	values
	(	@idfCase,
		@idfsCaseStatus,
		@idfsCaseProgressStatus,
		@idfsShowDiagnosis,
		@idfsCaseType,
		@idfOutbreak,
		@datEnteredDate,
		@strCaseID
	)
end

-- Update reference parameters related to tlbHumanCase

if not exists	(
	select	*
	from	trtBaseReference FinalState
	where	FinalState.idfsBaseReference = @idfsFinalState
			and FinalState.idfsReferenceType = 19000035	-- rftFinalState
			and FinalState.intRowStatus = 0
				)
begin
	set @idfsFinalState = null
end

if not exists	(
	select	*
	from	trtBaseReference HospitalizationStatus
	where	HospitalizationStatus.idfsBaseReference = @idfsHospitalizationStatus
			and HospitalizationStatus.idfsReferenceType = 19000041	-- rftHospStatus
			and HospitalizationStatus.intRowStatus = 0
				)
begin
	set @idfsHospitalizationStatus = null
end

if not exists	(
	select	*
	from	trtBaseReference HumanAgeType
	where	HumanAgeType.idfsBaseReference = @idfsHumanAgeType
			and HumanAgeType.idfsReferenceType = 19000042	-- rftHumanAgeType
			and HumanAgeType.intRowStatus = 0
				)
begin
	set @idfsHumanAgeType = null
end

if not exists	(
	select	*
	from	trtBaseReference YesNoValue
	where	YesNoValue.idfsBaseReference = @idfsYNAntimicrobialTherapy
			and YesNoValue.idfsReferenceType = 19000100	-- rftYesNoValue
			and YesNoValue.intRowStatus = 0
				)
begin
	set @idfsYNAntimicrobialTherapy = null
end

if not exists	(
	select	*
	from	trtBaseReference YesNoValue
	where	YesNoValue.idfsBaseReference = @idfsYNHospitalization
			and YesNoValue.idfsReferenceType = 19000100	-- rftYesNoValue
			and YesNoValue.intRowStatus = 0
				)
begin
	set @idfsYNHospitalization = null
end

if not exists	(
	select	*
	from	trtBaseReference YesNoValue
	where	YesNoValue.idfsBaseReference = @idfsYNSpecimenCollected
			and YesNoValue.idfsReferenceType = 19000100	-- rftYesNoValue
			and YesNoValue.intRowStatus = 0
				)
begin
	set @idfsYNSpecimenCollected = null
end

if not exists	(
	select	*
	from	trtBaseReference YesNoValue
	where	YesNoValue.idfsBaseReference = @idfsYNRelatedToOutbreak
			and YesNoValue.idfsReferenceType = 19000100	-- rftYesNoValue
			and YesNoValue.intRowStatus = 0
				)
begin
	set @idfsYNRelatedToOutbreak = null
end

if not exists	(
	select	*
	from	trtBaseReference Outcome
	where	Outcome.idfsBaseReference = @idfsOutcome
			and Outcome.idfsReferenceType = 19000064	-- rftOutcome
			and Outcome.intRowStatus = 0
				)
begin
	set @idfsOutcome = null
end

if not exists	(
	select	*
	from	tlbOffice Office
	where	Office.idfOffice = @idfSentByOffice
			and Office.intRowStatus = 0
				)
begin
	set @idfSentByOffice = null
end

if not exists	(
	select	*
	from	tlbOffice Office
	where	Office.idfOffice = @idfReceivedByOffice
			and Office.intRowStatus = 0
				)
begin
	set @idfReceivedByOffice = null
end

if not exists	(
	select	*
	from	tlbOffice Office
	where	Office.idfOffice = @idfInvestigatedByOffice
			and Office.intRowStatus = 0
				)
begin
	set @idfInvestigatedByOffice = null
end

if not exists	(
	select	*
	from	tlbGeoLocation GeoLocation
	where	GeoLocation.idfGeoLocation = @idfPointGeoLocation
				)
begin
	insert into	tlbGeoLocation
	(	idfGeoLocation
	)
	values
	(	@idfPointGeoLocation
	)
end

-- Post Epi & CS observations
exec spObservation_Post @idfEpiObservation, @idfsEPIFormTemplate
exec spObservation_Post @idfCSObservation, @idfsCSFormTemplate

-- Post tlbHumanCase
if exists	(
	select	*
	from	tlbHumanCase
	where	idfHumanCase = @idfCase
			)
begin
	update	tlbHumanCase
	set		idfsFinalState				=	@idfsFinalState,
			idfsHospitalizationStatus	=	@idfsHospitalizationStatus,
			idfsHumanAgeType			=	@idfsHumanAgeType,
			idfsYNAntimicrobialTherapy	=	@idfsYNAntimicrobialTherapy,
			idfsYNHospitalization		=	@idfsYNHospitalization,
			idfsYNSpecimenCollected		=	@idfsYNSpecimenCollected,
			idfsYNRelatedToOutbreak		=	@idfsYNRelatedToOutbreak,
			idfsOutcome					=	@idfsOutcome,
			idfsTentativeDiagnosis		=	@idfsTentativeDiagnosis,
			idfsFinalDiagnosis			=	@idfsFinalDiagnosis,
			idfsInitialCaseStatus		=	@idfsInitialCaseStatus,
			idfsFinalCaseStatus			=	@idfsFinalCaseStatus,
			idfSentByOffice				=	@idfSentByOffice,
			idfReceivedByOffice			=	@idfReceivedByOffice,
			idfInvestigatedByOffice		=	@idfInvestigatedByOffice,
			idfPointGeoLocation			=	@idfPointGeoLocation,
			idfEpiObservation			=	@idfEpiObservation,
			idfCSObservation			=	@idfCSObservation,
			datNotificationDate			=	@datNotificationDate,
			datCompletionPaperFormDate	=	@datCompletionPaperFormDate,
			datFirstSoughtCareDate		=	@datFirstSoughtCareDate,
			datModificationDate			=	@datModificationDate,
			datHospitalizationDate		=	@datHospitalizationDate,
			datFacilityLastVisit		=	@datFacilityLastVisit,
			datExposureDate				=	@datExposureDate,
			datDischargeDate			=	@datDischargeDate,
			datOnsetDate				=	@datOnsetDate,
			datInvestigationStartDate	=	@datInvestigationStartDate,
			datTentativeDiagnosisDate	=	@datTentativeDiagnosisDate,
			datFinalDiagnosisDate		=	@datFinalDiagnosisDate,
			strNote						=	@strNote,
			strCurrentLocation			=	@strCurrentLocation,
			strHospitalizationPlace		=	@strHospitalizationPlace,
			strLocalIdentifier			=	@strLocalIdentifier,
			strSoughtCareFacility		=	@strSoughtCareFacility,
			strSentByFirstName			=	@strSentByFirstName,
			strSentByPatronymicName		=	@strSentByPatronymicName,
			strSentByLastName			=	@strSentByLastName,
			strReceivedByFirstName		=	@strReceivedByFirstName,
			strReceivedByPatronymicName	=	@strReceivedByPatronymicName,
			strReceivedByLastName		=	@strReceivedByLastName,
			strEpidemiologistsName		=	@strEpidemiologistsName,
			strNotCollectedReason		=	@strNotCollectedReason,
			strClinicalDiagnosis		=	@strClinicalDiagnosis,
			intPatientAge				=	@intPatientAge,
			blnClinicalDiagBasis		=	@blnClinicalDiagBasis,
			blnLabDiagBasis				=	@blnLabDiagBasis,
			blnEpiDiagBasis				=	@blnEpiDiagBasis,
			strClinicalNotes			=	@strClinicalNotes,
			strSummaryNotes				=	@strSummaryNotes

	where	idfHumanCase = @idfCase
end
else begin
	insert into	tlbHUmanCase
	(	idfHumanCase,
		idfsFinalState,
		idfsHospitalizationStatus,
		idfsHumanAgeType,
		idfsYNAntimicrobialTherapy,
		idfsYNHospitalization,
		idfsYNSpecimenCollected,
		idfsYNRelatedToOutbreak,
		idfsOutcome,
		idfsTentativeDiagnosis,
		idfsFinalDiagnosis,
		idfsInitialCaseStatus,
		idfsFinalCaseStatus,
		idfSentByOffice,
		idfReceivedByOffice,
		idfInvestigatedByOffice,
		idfPointGeoLocation,
		idfEpiObservation,
		idfCSObservation,
		datNotificationDate,
		datCompletionPaperFormDate,
		datFirstSoughtCareDate,
		datModificationDate,
		datHospitalizationDate,
		datFacilityLastVisit,
		datExposureDate,
		datDischargeDate,
		datOnsetDate,
		datInvestigationStartDate,
		datTentativeDiagnosisDate,
		datFinalDiagnosisDate,
		strNote,
		strCurrentLocation,
		strHospitalizationPlace,
		strLocalIdentifier,
		strSoughtCareFacility,
		strSentByFirstName,
		strSentByPatronymicName,
		strSentByLastName,
		strReceivedByFirstName,
		strReceivedByPatronymicName,
		strReceivedByLastName,
		strEpidemiologistsName,
		strNotCollectedReason,
		strClinicalDiagnosis,
		intPatientAge,
		blnClinicalDiagBasis,
		blnLabDiagBasis,
		blnEpiDiagBasis,
		strClinicalNotes,
		strSummaryNotes
	)
	values
	(	@idfCase,
		@idfsFinalState,
		@idfsHospitalizationStatus,
		@idfsHumanAgeType,
		@idfsYNAntimicrobialTherapy,
		@idfsYNHospitalization,
		@idfsYNSpecimenCollected,
		@idfsYNRelatedToOutbreak,
		@idfsOutcome,
		@idfsTentativeDiagnosis,
		@idfsFinalDiagnosis,
		@idfsInitialCaseStatus,
		@idfsFinalCaseStatus,
		@idfSentByOffice,
		@idfReceivedByOffice,
		@idfInvestigatedByOffice,
		@idfPointGeoLocation,
		@idfEpiObservation,
		@idfCSObservation,
		@datNotificationDate,
		@datCompletionPaperFormDate,
		@datFirstSoughtCareDate,
		@datModificationDate,
		@datHospitalizationDate,
		@datFacilityLastVisit,
		@datExposureDate,
		@datDischargeDate,
		@datOnsetDate,
		@datInvestigationStartDate,
		@datTentativeDiagnosisDate,
		@datFinalDiagnosisDate,
		@strNote,
		@strCurrentLocation,
		@strHospitalizationPlace,
		@strLocalIdentifier,
		@strSoughtCareFacility,
		@strSentByFirstName,
		@strSentByPatronymicName,
		@strSentByLastName,
		@strReceivedByFirstName,
		@strReceivedByPatronymicName,
		@strReceivedByLastName,
		@strEpidemiologistsName,
		@strNotCollectedReason,
		@strClinicalDiagnosis,
		@intPatientAge,
		@blnClinicalDiagBasis,
		@blnLabDiagBasis,
		@blnEpiDiagBasis,
		@strClinicalNotes,
		@strSummaryNotes
	)
end

if exists	(
	select		*
	from		tlbHuman
	inner join	tlbParty
	on			tlbParty.idfParty = tlbHuman.idfHuman
				and tlbParty.intRowStatus = 0
	where		tlbHuman.idfHuman = @idfHuman
			)
begin
	-- Post tlbParty
	update	tlbParty
	set		idfCase	= @idfCase
	where	idfParty = @idfHuman

	-- Update reference parameters related to tlbHuman
	if not exists	(
			select	*
			from	trtBaseReference OccupationType
			where	OccupationType.idfsBaseReference = @idfsOccupationType
					and OccupationType.idfsReferenceType = 19000061	-- rftOccupationType
					and OccupationType.intRowStatus = 0
					)
	begin
		set	@idfsOccupationType = null
	end

	if not exists	(
		select	*
		from	tlbGeoLocation GeoLocation
		where	GeoLocation.idfGeoLocation = @idfRegistrationAddress
				and GeoLocation.idfsGeoLocationType = 10036001		-- Address
				and GeoLocation.intRowStatus = 0
					)
	begin
		set @idfRegistrationAddress = null
	end

	-- Post tlbHuman
	update	tlbHuman
	set		idfsOccupationType		= @idfsOccupationType,
			idfRegistrationAddress	= @idfRegistrationAddress,
			datDateOfDeath			= @datDateOfDeath,
			strRegistrationPhone	= @strRegistrationPhone,
			strWorkPhone			= @strWorkPhone
	where	idfHuman = @idfHuman
end

GO
SET QUOTED_IDENTIFIER OFF 
GO
SET ANSI_NULLS ON 
GO

